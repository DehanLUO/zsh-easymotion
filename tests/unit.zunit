#!/usr/bin/env zunit

@setup {
  load ../init.zsh
}

# 1
@test 'assign_keys: empty indices returns empty keys' {
  local out
  _zsh_easymotion_assign_keys \
    ""                        \
    out
  assert "$out" is_empty
}

# 2
@test 'assign_keys: single target uses first key' {
  local out
  _zsh_easymotion_assign_keys \
    "10"                      \
    out
  assert "$out" same_as "a"
}

# 3
@test 'assign_keys: only single keys when enough' {
  # Default keys = a-z + ; → 27 keys
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4 5"               \
    out
  assert "$out" same_as "a b c d e"
}

# 4
@test 'assign_keys: mixed single and double keys' {
  # Mock short key set for clarity
  zstyle ':zsh-easymotion:*' keys 'abc'
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4 5 6"             \
    out
  
  # K=3, need P=2 → singles: [a], doubles: [ba bb bc ca cb cc]
  # Take first 6: a ba bb bc ca cb
  assert "$out" same_as "a ba bb bc ca cb"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 5
@test 'assign_keys: all double keys (max capacity)' {
  zstyle ':zsh-easymotion:*' keys 'ab'  # K=2, max=4
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4"                 \
    out
  
  # P=2 → singles: none, doubles: [aa ab ba bb]
  assert "$out" same_as "aa ab ba bb"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 6
@test 'assign_keys: fails when target count exceeds capacity' {
  zstyle ':zsh-easymotion:*' keys 'ab'  # max=4
  
  local out
  run _zsh_easymotion_assign_keys \
    "1 2 3 4 5"                   \
    out
  assert $state equals 1

  zstyle -d ':zsh-easymotion:*' keys
}

# 7
@test 'assign_keys: respects custom key set from zstyle' {
  zstyle ':zsh-easymotion:*' keys 'xyz;'
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4"                 \
    out
  assert "$out" same_as "x y z ;"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 8
@test 'boundaries: empty buffer' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    ""                       \
    out
  assert "$out" same_as ""

  _zsh_easymotion_boundaries \
    "end"                    \
    ""                       \
    out
  assert "$out" same_as ""
}

# 9
@test 'boundaries: basic words' {
  local out
  _zsh_easymotion_boundaries \
   "word"                    \
   "abc def ghi"             \
   out
  assert "$out" same_as "1 5 9"

  _zsh_easymotion_boundaries \
    "end"                    \
    "abc def ghi"            \
    out
  assert "$out" same_as "3 7 11"
}

# 10
@test 'boundaries: leading and trailing spaces' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "  hello  world  "       \
    out
  assert "$out" same_as "3 10"

  _zsh_easymotion_boundaries \
    "end"                    \
    "  hello  world  "       \
    out
  assert "$out" same_as "7 14"
}

# 11
@test 'boundaries: punctuation as separators' {
  # Words: "foo", "bar_baz" (underscore is part of word!)
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "foo.bar_baz"            \
    out
  assert "$out" same_as "1 5"

  _zsh_easymotion_boundaries \
    "end"                    \
    "foo.bar_baz"            \
    out
  assert "$out" same_as "3 11"
}

# 12
@test 'boundaries: alnum and underscore form words' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "a1_b2 c3"               \
    out
  assert "$out" same_as "1 7"

  _zsh_easymotion_boundaries \
    "end"                    \
    "a1_b2 c3"               \
    out
  assert "$out" same_as "5 8"
}

# 13
@test 'boundaries: only non-word characters' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "!!!   ... ;;;"          \
    out
  assert "$out" same_as ""

  _zsh_easymotion_boundaries \
    "end"                    \
    "!!!   ... ;;;"          \
    out
  assert "$out" same_as ""
}

# 14
@test 'boundaries: single-character words' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "a b c"                  \
    out
  assert "$out" same_as "1 3 5"

  _zsh_easymotion_boundaries \
    "end"                    \
    "a b c"                  \
    out
  assert "$out" same_as "1 3 5"
}

# 15
@test 'build_keymap: empty input' {
  local out
  _zsh_easymotion_build_keymap \
  ""                           \
  out
  assert "$out" same_as ""
}

# 16
@test 'build_keymap: single entry with mock' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="x"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "5"                        \
    out
  assert "$out" same_as "x"$'\0'"5"
}

# 17
@test 'build_keymap: multiple entries with mock' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="x y"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "2 4"                      \
    out
  assert "$out" same_as "x"$'\0'"2 y"$'\0'"4"
}

# 18
@test 'build_keymap: multi-character keys with mock' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="a b ;a ;b"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "3 5 7 11"                 \
    out

  local -a arr_out
  : ${(A)arr_out::=${(s. .)out}}
  assert "${#arr_out[@]}" equals 4
  assert "$arr_out[1]" same_as "a"$'\0'"3"
  assert "$arr_out[2]" same_as "b"$'\0'"5"
  assert "$arr_out[3]" same_as ";a"$'\0'"7"
  assert "$arr_out[4]" same_as ";b"$'\0'"11"
}

# 19
@test 'build_keymap: propagates assign_keys failure with mock' {
  local _fn_assign_keys=_zsh_easymotion_failure

  local out
  run _zsh_easymotion_build_keymap \
    "1"                            \
    out
  assert $state equals 1
}

# 20
@test 'char2regex: default mode' {
  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as 'a'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as 'A'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'
}

# 21
@test 'char2regex: ignorecase mode' {
  zstyle ':zsh-easymotion:*' search-case ignorecase

  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'

  zstyle -d ':zsh-easymotion:*' search-case
}

# 22
@test 'char2regex: smartcase mode' {
  zstyle ':zsh-easymotion:*' search-case smartcase

  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as 'A'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'

  zstyle -d ':zsh-easymotion:*' search-case
}

# 23
@test 'char2regex: regex metacharacters always escaped in literal context' {
  local out
  _zsh_easymotion_char2regex \
    '^'                      \
    out
  assert "$out" same_as '\^'

  _zsh_easymotion_char2regex \
    '|'                      \
    out
  assert "$out" same_as '\|'

  _zsh_easymotion_char2regex \
    '('                      \
    out
  assert "$out" same_as '\('

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['
}

# 24
@test 'failure: returns exit code 1' {
  run _zsh_easymotion_failure
  assert $state equals 1
}

# 25
@test 'handle_jump: fails when no targets with mock' {
  local _fn_move_curosr=_zsh_easymotion_success
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=_zsh_easymotion_success
  local _fn_handle_jump=_zsh_easymotion_success

  run _zsh_easymotion_handle_jump \
    ""                            \
    ""
  assert $state equals 1
}

# 26
@test 'handle_jump: cancel jump with mock' {
  local _fn_move_curosr=_zsh_easymotion_success
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=_zsh_easymotion_failure
  local _fn_handle_jump=_zsh_easymotion_success

  run _zsh_easymotion_handle_jump \
    "abc"                         \
    ""                            \
    "a"$'\0'"5"                   \
    "b"$'\0'"6"

  assert $state equals 1
}

# 27
@test 'handle_jump: single target jumps directly with mock' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=_zsh_easymotion_success
  local _fn_handle_jump=_zsh_easymotion_success

  _zsh_easymotion_handle_jump \
    "abc"                     \
    ""                        \
    "a"$'\0'"5"

  assert "$out" same_as "5"
}

# 28
@test 'handle_jump: recursive call with filtered keymaps with mock' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  function mock_prompt_jump() {
    local output="$1"; shift
    : ${(P)output::="a"}
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=mock_prompt_jump

  local -a maps=(
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  _zsh_easymotion_handle_jump \
    "buf"                     \
    ";"                       \
    ${maps[@]}

  assert "$out" same_as "11"
}

# 29
@test 'handle_jump: recursive call with mock' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  local flag
  function mock_prompt_jump() {
    local output="$1"; shift

    if [[ -z "$flag" ]]; then
      : ${(P)output::=";"}
      flag=true
    else
      : ${(P)output::="b"}
    fi
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=mock_prompt_jump

  local -a maps=(
    "a"$'\0'"7"
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  _zsh_easymotion_handle_jump \
    "buf"                     \
    ""                        \
    ${maps[@]}

  assert "$out" same_as "13"
}

# 30
@test 'handle_jump: typing unmatched key leads to failure with mock' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  function mock_prompt_jump() {
    local output="$1"; shift
    : ${(P)output::="b"}
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_render_jump_markers=_zsh_easymotion_success
  local _fn_prompt_jump=mock_prompt_jump

  local -a maps=(
    "a"$'\0'"7"
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  run _zsh_easymotion_handle_jump \
    "buf"                         \
    ""                            \
    ${maps[@]}

  assert $state equals 1
}

# 31
@test 'invoke: failure with mock' {
  local _fn_jump_indices=_zsh_easymotion_failure
  local _fn_build_keymap=_zsh_easymotion_success
  local _fn_handle_jump=_zsh_easymotion_success

  run _zsh_easymotion_invoke \
    ""                       \
    ""
  assert $state equals 1

  local _fn_jump_indices=_zsh_easymotion_success
  local _fn_build_keymap=_zsh_easymotion_failure
  local _fn_handle_jump=_zsh_easymotion_success

  run _zsh_easymotion_invoke \
    ""                       \
    ""
  assert $state equals 1

  local _fn_jump_indices=_zsh_easymotion_success
  local _fn_build_keymap=_zsh_easymotion_success
  local _fn_handle_jump=_zsh_easymotion_failure

  run _zsh_easymotion_invoke \
    ""                       \
    ""
  assert $state equals 1
}

# 32
@test 'invoke: success with mock' {
  local _fn_jump_indices=_zsh_easymotion_success
  local _fn_build_keymap=_zsh_easymotion_success
  local _fn_handle_jump=_zsh_easymotion_success

  run _zsh_easymotion_invoke \
    ""                       \
    ""
  assert $state equals 0
}

# 33
@test 'isprintable: single printable char returns true' {
  run _zsh_easymotion_isprintable 'a'
  assert $state equals 0

  run _zsh_easymotion_isprintable 'Z'
  assert $state equals 0

  run _zsh_easymotion_isprintable '5'
  assert $state equals 0

  run _zsh_easymotion_isprintable '!'
  assert $state equals 0

  run _zsh_easymotion_isprintable ' '
  assert $state equals 0
}

# 34
@test 'isprintable: empty string returns false' {
  run _zsh_easymotion_isprintable ""
  assert $state equals 1
}

# 35
@test 'isprintable: control characters return false' {
  run _zsh_easymotion_isprintable $'\n'
  assert $state equals 1

  run _zsh_easymotion_isprintable $'\t'
  assert $state equals 1
  run _zsh_easymotion_isprintable $'\r'
  assert $state equals 1

  run _zsh_easymotion_isprintable $'\0'
  assert $state equals 1
}

# 36
@test 'jump_indices: boundaries failure with mock' {
  local _fn_boundaries=_zsh_easymotion_failure
  local _fn_prompt_search=_zsh_easymotion_success
  local _fn_char2regex=_zsh_easymotion_success
  local _fn_match_indices=_zsh_easymotion_success

  local out
  run _zsh_easymotion_jump_indices \
    "word"                         \
    "hello world"                  \
    out
  assert $state equals 1

  run _zsh_easymotion_jump_indices \
    "end"                          \
    "hello world"                  \
    out
  assert $state equals 1
}

# 37
@test 'jump_indices: search failure with mock' {
  local _fn_boundaries=_zsh_easymotion_success
  local _fn_prompt_search=_zsh_easymotion_failure
  local _fn_char2regex=_zsh_easymotion_success
  local _fn_match_indices=_zsh_easymotion_success

  local out
  run _zsh_easymotion_jump_indices \
    "search"                       \
    "hello world"                  \
    out
  assert $state equals 1
}

# 38
@test 'jump_indices: boundaries with mock' {
  local called=0
  function mock_easymotion_boundaries() {
    called=1
    : ${(P)3::="1 7"}
  }

  local uncalled=0
  function mock_match_indices() {
    uncalled=1
  }

  local _fn_boundaries=mock_easymotion_boundaries
  local _fn_prompt_search=_zsh_easymotion_success
  local _fn_char2regex=_zsh_easymotion_success
  local _fn_match_indices=_zsh_easymotion_success

  local out
  _zsh_easymotion_jump_indices \
    "word"                     \
    "hello world"              \
    out
  assert $called equals 1
  assert $uncalled equals 0
  assert "$out" same_as "1 7"

  called=0
  uncalled=0
  out=""
  _zsh_easymotion_jump_indices \
    "end"                      \
    "hello world"              \
    out
  assert $called equals 1
  assert $uncalled equals 0
  assert "$out" same_as "1 7"
}

# 39
@test 'jump_indices: search with mock' {
  local called=0
  function mock_easymotion_boundaries() {
    called=1
    : ${(P)3::="7 11"}
  }

  local uncalled=0
  function mock_boundaries() {
    uncalled=1
  }

  local _fn_boundaries=mock_boundaries
  local _fn_prompt_search=_zsh_easymotion_success
  local _fn_char2regex=_zsh_easymotion_success
  local _fn_match_indices=mock_easymotion_boundaries

  local out
  _zsh_easymotion_jump_indices \
    "search"                   \
    "hello world"              \
    out
  assert $called equals 1
  assert $uncalled equals 0
  assert "$out" same_as "7 11"
}

# 40
@test 'match_indices: empty buffer returns empty' {
  local out
  _zsh_easymotion_match_indices \
    'x'                         \
    ""                          \
  out
  assert "$out" is_empty
}

# 41
@test 'match_indices: basic literal pattern' {
  local out
  _zsh_easymotion_match_indices \
    '/'                         \
    "a/b/c"                     \
    out
  assert "$out" same_as "2 4"
}

# 42
@test 'match_indices: no matches returns empty' {
  local out
  _zsh_easymotion_match_indices \
    'z'                         \
    "abc"                       \
    out
  assert "$out" is_empty
}

# 43
@test 'match_indices: matches at start and end' {
  local out
  _zsh_easymotion_match_indices \
    'a'                         \
    "abcba"                     \
    out
  assert "$out" same_as "1 5"
}

# 44
@test 'match_indices: regex metacharacters interpreted as pattern' {
  local out
  _zsh_easymotion_match_indices \
    "[aA]"                      \
    "An apple a day"            \
    out
  assert "$out" same_as "1 4 10 13"
}

# 45
@test 'match_indices: consecutive matches found correctly' {
  local out
  _zsh_easymotion_match_indices \
    'a'                         \
    "aaa"                       \
    out
  assert "$out" same_as "1 2 3"
}

# 46
@test 'move_cursor: converts 1-based to 0-based correctly' {
  local CURSOR

  _zsh_easymotion_move_cursor 1
  assert "$CURSOR" same_as "0"

  _zsh_easymotion_move_cursor 5
  assert "$CURSOR" same_as "4"

  _zsh_easymotion_move_cursor 10
  assert "$CURSOR" same_as "9"
}

# 47
@test 'prompt_jump: uses custom prompt from zstyle with mock' {
  zstyle ':zsh-easymotion:*' prompt-key "Jump to> "

  local captured_prompt=""
  function mock_query_char() {
    captured_prompt="$2"
    : ${(P)1::="x"}  # mock user input
  }

  local _fn_query_char=mock_query_char

  local out
  _zsh_easymotion_prompt_jump out

  assert "$captured_prompt" same_as "Jump to> "
  assert "$out" same_as "x"

  zstyle -d ':zsh-easymotion:*' prompt-key
}

# 48
@test 'prompt_search: uses custom prompt from zstyle with mock' {
  zstyle ':zsh-easymotion:*' prompt-char "Search > "

  local captured_prompt=""
  function mock_query_char() {
    captured_prompt="$2"
    : ${(P)1::="x"}  # mock user input
  }

  local _fn_query_char=mock_query_char

  local out
  _zsh_easymotion_prompt_search out

  assert "$captured_prompt" same_as "Search > "
  assert "$out" same_as "x"

  zstyle -d ':zsh-easymotion:*' prompt-char
}

# 49
@test 'protected_run: fallback with mock' {
  local BUFFER="Hello, World!"
  local CURSOR=10

  function zle() {}

  function mock_invoke_fail() {
    BUFFER="mock"
    CURSOR=20
    _zsh_easymotion_failure
  }
  local _fn_invoke=mock_invoke_fail

  _zsh_easymotion_protected_run "search"
  assert "$BUFFER" same_as "Hello, World!"
  assert $CURSOR equals 10
}

# 50
@test 'protected_run: success preserves cursor with mock' {
  local BUFFER="Hello, World!"
  local CURSOR=10

  function zle() {}

  function mock_invoke() {
    BUFFER="mock"
    CURSOR=20
  }
  local _fn_invoke=mock_invoke

  _zsh_easymotion_protected_run "search"
  assert "$BUFFER" same_as "Hello, World!"
  assert $CURSOR equals 20
}

# 51
@test 'query_char: failure readkey with mock' {
  local _fn_readkey=_zsh_easymotion_failure

  local out
  run _zsh_easymotion_query_char \
    "out"                        \
    "prompt"                     \
    ""
  assert $state equals 1
}

# 52
@test 'query_char: readkey with mock' {
  function mock_readkey() {
    : ${(P)1::="a"}
  }
  local _fn_readkey=mock_readkey

  local out
  _zsh_easymotion_query_char \
    "out"                    \
    "prompt"                 \
    ""
  assert "$out" same_as "a"
}

# 53
@test 'readkey: fails on eof with mock' {
  function read() {
    _zsh_easymotion_failure
  }

  local out
  run _zsh_easymotion_readkey out

  assert $state equals 1
}

# 54
@test 'readkey: not printable with mock' {
  function read() {}

  local _fn_isprintable=_zsh_easymotion_failure

  local out
  run _zsh_easymotion_readkey out

  assert $state equals 1
}

# 55
@test 'readkey: assign with mock' {
  function read() {
    local ref="$4"
    : ${(P)ref::="é"}  # mock user input
  }

  local _fn_isprintable=_zsh_easymotion_success

  local out
  _zsh_easymotion_readkey out

  assert "$out" same_as "é"
}

# 56
@test 'render_jump_markers: empty input' {
  function zle() {}

  local -a region_highlight
  _zsh_easymotion_render_jump_markers \
    "hello"                           \
    "" 
  assert "${#region_highlight[@]}" equals 1
  assert "$region_highlight[1]" same_as "0 5 fg=black,bold"

  # restores original buffer
  assert "$BUFFER" same_as "hello"
}

# 57
@test 'render_jump_markers: single-character' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a map=(
    "a"$'\0'"2"
    "b"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/x"                           \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 5 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=196,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=196,bold"
  assert "$BUFFER" same_as "xaxbx"
}

# 58
@test 'render_jump_markers: multi-character' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a map=("ab"$'\0'"2")
  _zsh_easymotion_render_jump_markers \
    "x/xx"                            \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 4 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$BUFFER" same_as "xabx"
}

# 59
@test 'render_jump_markers: mixed single and multi-char' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a maps=(
    "a"$'\0'"2"
    "bc"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/xx"                          \
    ""                                \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 4
  assert "$region_highlight[1]" same_as "0 6 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=196,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=208,bold"
  assert "$region_highlight[4]" same_as "4 5 fg=94,bold"
  assert "$BUFFER" same_as "xaxbcx"
}

# 60
@test 'render_jump_markers: overlapping' {
  function zle() {}

  local BUFFER=""
  local -a region_highlight
  local -a maps=(
    ";a"$'\0'"2"
    ";b"$'\0'"3"
  )
  _zsh_easymotion_render_jump_markers \
    "x//x"                            \
    ""                                \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 5
  assert "$region_highlight[1]" same_as "0 4 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$region_highlight[4]" same_as "2 3 fg=208,bold"
  assert "$region_highlight[5]" same_as "3 4 fg=94,bold"
  assert "$BUFFER" same_as "x;;b"
}

# 61
@test 'render_jump_markers: end of buffer' {
  function zle() {}

  local BUFFER=""
  local -a region_highlight
  local -a maps=(
    ";a"$'\0'"2"
  )
  _zsh_easymotion_render_jump_markers \
    "x/"                              \
    ""                                \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 2 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$BUFFER" same_as "x;a"
}

# 62
@test 'render_jump_markers: respects custom zstyle colors' {
  zstyle ':zsh-easymotion:*' fg-primary 'fg=red,bold'
  zstyle ':zsh-easymotion:*' fg-secondary 'fg=orange,bold'
  zstyle ':zsh-easymotion:*' fg-tertiary 'fg=yellow,bold'
  zstyle ':zsh-easymotion:*' fg-dim 'fg=green,bold'

  function zle() {}

  local -a region_highlight
  local -a map=(
    "a"$'\0'"2"
    ";;"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/x"                           \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 4
  assert "$region_highlight[1]" same_as "0 5 fg=green,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=red,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=orange,bold"
  assert "$region_highlight[4]" same_as "4 5 fg=yellow,bold"

  zstyle -d ':zsh-easymotion:*' fg-primary
  zstyle -d ':zsh-easymotion:*' fg-secondary
  zstyle -d ':zsh-easymotion:*' fg-tertiary
  zstyle -d ':zsh-easymotion:*' fg-dim
}

# 63
@test 'success: returns exit code 0' {
  run _zsh_easymotion_success
  assert $state equals 0
}
