#!/usr/bin/env zunit

@setup {
  load ../init.zsh
}

# 1
@test 'assign_keys: empty indices returns empty keys' {
  local out
  _zsh_easymotion_assign_keys \
    ""                        \
    out
  assert "$out" is_empty
}

# 2
@test 'assign_keys: single target uses first key' {
  local out
  _zsh_easymotion_assign_keys \
    "10"                      \
    out
  assert "$out" same_as "a"
}

# 3
@test 'assign_keys: only single keys when enough' {
  # Default keys = a-z + ; → 27 keys
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4 5"               \
    out
  assert "$out" same_as "a b c d e"
}

# 4
@test 'assign_keys: mixed single and double keys' {
  # Mock short key set for clarity
  zstyle ':zsh-easymotion:*' keys 'abc'
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4 5 6"             \
    out
  
  # K=3, need P=2 → singles: [a], doubles: [ba bb bc ca cb cc]
  # Take first 6: a ba bb bc ca cb
  assert "$out" same_as "a ba bb bc ca cb"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 5
@test 'assign_keys: all double keys (max capacity)' {
  zstyle ':zsh-easymotion:*' keys 'ab'  # K=2, max=4
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4"                 \
    out
  
  # P=2 → singles: none, doubles: [aa ab ba bb]
  assert "$out" same_as "aa ab ba bb"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 6
@test 'assign_keys: fails when target count exceeds capacity' {
  zstyle ':zsh-easymotion:*' keys 'ab'  # max=4
  
  local out
  run _zsh_easymotion_assign_keys \
    "1 2 3 4 5"                   \
    out
  assert $state equals 1

  zstyle -d ':zsh-easymotion:*' keys
}

# 7
@test 'assign_keys: respects custom key set from zstyle' {
  zstyle ':zsh-easymotion:*' keys 'xyz;'
  
  local out
  _zsh_easymotion_assign_keys \
    "1 2 3 4"                 \
    out
  assert "$out" same_as "x y z ;"
  
  zstyle -d ':zsh-easymotion:*' keys
}

# 8
@test 'boundaries: empty buffer' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    ""                       \
    out
  assert "$out" same_as ""

  _zsh_easymotion_boundaries \
    "end"                    \
    ""                       \
    out
  assert "$out" same_as ""
}

# 9
@test 'boundaries: basic words' {
  local out
  _zsh_easymotion_boundaries \
   "word"                    \
   "abc def ghi"             \
   out
  assert "$out" same_as "1 5 9"

  _zsh_easymotion_boundaries \
    "end"                    \
    "abc def ghi"            \
    out
  assert "$out" same_as "3 7 11"
}

# 10
@test 'boundaries: leading and trailing spaces' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "  hello  world  "       \
    out
  assert "$out" same_as "3 10"

  _zsh_easymotion_boundaries \
    "end"                    \
    "  hello  world  "       \
    out
  assert "$out" same_as "7 14"
}

# 11
@test 'boundaries: punctuation as separators' {
  # Words: "foo", "bar_baz" (underscore is part of word!)
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "foo.bar_baz"            \
    out
  assert "$out" same_as "1 5"

  _zsh_easymotion_boundaries \
    "end"                    \
    "foo.bar_baz"            \
    out
  assert "$out" same_as "3 11"
}

# 12
@test 'boundaries: alnum and underscore form words' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "a1_b2 c3"               \
    out
  assert "$out" same_as "1 7"

  _zsh_easymotion_boundaries \
    "end"                    \
    "a1_b2 c3"               \
    out
  assert "$out" same_as "5 8"
}

# 13
@test 'boundaries: only non-word characters' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "!!!   ... ;;;"          \
    out
  assert "$out" same_as ""

  _zsh_easymotion_boundaries \
    "end"                    \
    "!!!   ... ;;;"          \
    out
  assert "$out" same_as ""
}

# 14
@test 'boundaries: single-character words' {
  local out
  _zsh_easymotion_boundaries \
    "word"                   \
    "a b c"                  \
    out
  assert "$out" same_as "1 3 5"

  _zsh_easymotion_boundaries \
    "end"                    \
    "a b c"                  \
    out
  assert "$out" same_as "1 3 5"
}

# 15
@test 'build_keymap: empty input' {
  local out
  _zsh_easymotion_build_keymap \
  ""                           \
  out
  assert "$out" same_as ""
}

# 16
@test 'build_keymap: mock single entry' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="x"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "5"                        \
    out
  assert "$out" same_as "x"$'\0'"5"
}

# 17
@test 'build_keymap: mock multiple entries' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="x y"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "2 4"                      \
    out
  assert "$out" same_as "x"$'\0'"2 y"$'\0'"4"
}

# 18
@test 'build_keymap: mock multi-character keys' {
  function mock_assign_keys() {
    shift
    local output="$1"; shift
    : ${(P)output::="a b ;a ;b"}
  }

  local _fn_assign_keys=mock_assign_keys
  local out
  _zsh_easymotion_build_keymap \
    "3 5 7 11"                 \
    out

  local -a arr_out
  : ${(A)arr_out::=${(s. .)out}}
  assert "${#arr_out[@]}" equals 4
  assert "$arr_out[1]" same_as "a"$'\0'"3"
  assert "$arr_out[2]" same_as "b"$'\0'"5"
  assert "$arr_out[3]" same_as ";a"$'\0'"7"
  assert "$arr_out[4]" same_as ";b"$'\0'"11"
}

# 19
@test 'build_keymap: mock propagates assign_keys failure' {
  local _fn_assign_keys=_zsh_easymotion_failure

  local out
  run _zsh_easymotion_build_keymap \
    "1"                            \
    out
  assert $state equals 1
}

# 20
@test 'char2regex: default mode' {
  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as 'a'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as 'A'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'
}

# 21
@test 'char2regex: ignorecase mode' {
  zstyle ':zsh-easymotion:*' search-case ignorecase

  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'

  zstyle -d ':zsh-easymotion:*' search-case
}

# 22
@test 'char2regex: smartcase mode' {
  zstyle ':zsh-easymotion:*' search-case smartcase

  local out
  _zsh_easymotion_char2regex \
    'a'                      \
    out
  assert "$out" same_as '[aA]'

  _zsh_easymotion_char2regex \
    'A'                      \
    out
  assert "$out" same_as 'A'

  _zsh_easymotion_char2regex \
    '.'                      \
    out
  assert "$out" same_as '.'

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['

  _zsh_easymotion_char2regex \
    '1'                      \
    out
  assert "$out" same_as '1'

  zstyle -d ':zsh-easymotion:*' search-case
}

# 23
@test 'char2regex: regex metacharacters always escaped in literal context' {
  local out
  _zsh_easymotion_char2regex \
    '^'                      \
    out
  assert "$out" same_as '\^'

  _zsh_easymotion_char2regex \
    '|'                      \
    out
  assert "$out" same_as '\|'

  _zsh_easymotion_char2regex \
    '('                      \
    out
  assert "$out" same_as '\('

  _zsh_easymotion_char2regex \
    '['                      \
    out
  assert "$out" same_as '\['
}

# 24
@test 'failure: returns exit code 1' {
  run _zsh_easymotion_failure
  assert $state equals 1
}

# 25
@test 'handle_jump: fails when no targets' {
  run _zsh_easymotion_handle_jump \
    ""                            \
    ""
  assert $state equals 1
}

# 26
@test 'handle_jump: mock cancel jump' {
  local _fn_prompt_jump=_zsh_easymotion_failure
  local _fn_render_jump_markers=":"

  run _zsh_easymotion_handle_jump \
    "abc"                         \
    ""                            \
    "a"$'\0'"5"                   \
    "b"$'\0'"6"

  assert $state equals 1
}

# 27
@test 'handle_jump: mock single target jumps directly' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  local _fn_move_cursor=mock_move_cursor

  _zsh_easymotion_handle_jump \
    "abc"                     \
    ""                        \
    "a"$'\0'"5"

  assert "$out" same_as "5"
}

# 28
@test 'handle_jump: mock recursive call with filtered keymaps' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  function mock_prompt_jump() {
    local output="$1"; shift
    : ${(P)output::="a"}
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_prompt_jump=mock_prompt_jump
  local _fn_render_jump_markers=":"

  local -a maps=(
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  _zsh_easymotion_handle_jump \
    "buf"                     \
    ";"                       \
    ${maps[@]}

  assert "$out" same_as "11"
}

# 29
@test 'handle_jump: mock recursive call' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  local flag
  function mock_prompt_jump() {
    local output="$1"; shift

    if [[ -z "$flag" ]]; then
      : ${(P)output::=";"}
      flag=true
    else
      : ${(P)output::="b"}
    fi
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_prompt_jump=mock_prompt_jump
  local _fn_render_jump_markers=":"

  local -a maps=(
    "a"$'\0'"7"
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  _zsh_easymotion_handle_jump \
    "buf"                     \
    ""                        \
    ${maps[@]}

  assert "$out" same_as "13"
}

# 30
@test 'handle_jump: typing unmatched key leads to failure' {
  local out
  function mock_move_cursor() {
    out="$1"
  }

  function mock_prompt_jump() {
    local output="$1"; shift
    : ${(P)output::="b"}
  }

  local _fn_move_cursor=mock_move_cursor
  local _fn_prompt_jump=mock_prompt_jump
    local _fn_render_jump_markers=":"

  local -a maps=(
    "a"$'\0'"7"
    ";a"$'\0'"11"
    ";b"$'\0'"13"
  )

  run _zsh_easymotion_handle_jump \
    "buf"                         \
    ""                            \
    ${maps[@]}

  assert $state equals 1
}

# 31
@test 'isprintable: single printable char returns true' {
  run _zsh_easymotion_isprintable 'a'
  assert $state equals 0

  run _zsh_easymotion_isprintable 'Z'
  assert $state equals 0

  run _zsh_easymotion_isprintable '5'
  assert $state equals 0

  run _zsh_easymotion_isprintable '!'
  assert $state equals 0

  run _zsh_easymotion_isprintable ' '
  assert $state equals 0
}

# 32
@test 'isprintable: empty string returns false' {
  run _zsh_easymotion_isprintable ""
  assert $state equals 1
}

# 33
@test 'isprintable: control characters return false' {
  run _zsh_easymotion_isprintable $'\n'
  assert $state equals 1

  run _zsh_easymotion_isprintable $'\t'
  assert $state equals 1
  run _zsh_easymotion_isprintable $'\r'
  assert $state equals 1

  run _zsh_easymotion_isprintable $'\0'
  assert $state equals 1
}

# 34
@test 'jump_indices: boundaries with mock' {
  local called=0
  function mock_easymotion_boundaries() {
    called=1
    : ${(P)3::="1 7"}
  }

  local _fn_boundaries=mock_easymotion_boundaries
  local _fn_prompt=_zsh_easymotion_success
  local _fn_prompt_search=_zsh_easymotion_success
  local _fn_char2regex=_zsh_easymotion_success
  local _fn_match_indices=_zsh_easymotion_success

  local out
  _zsh_easymotion_jump_indices \
    "word"                     \
    "hello world"              \
    out
  assert "$called" same_as "1"
  assert "$out" same_as "1 7"

  called=0
  out=""
  _zsh_easymotion_jump_indices \
    "end"                      \
    "hello world"              \
    out
  assert "$called" same_as "1"
  assert "$out" same_as "1 7"
}

# 35
@test 'match_indices: empty buffer returns empty' {
  local out
  _zsh_easymotion_match_indices \
    'x'                         \
    ""                          \
  out
  assert "$out" is_empty
}

# 36
@test 'match_indices: basic literal pattern' {
  local out
  _zsh_easymotion_match_indices \
    '/'                         \
    "a/b/c"                     \
    out
  assert "$out" same_as "2 4"
}

# 37
@test 'match_indices: no matches returns empty' {
  local out
  _zsh_easymotion_match_indices \
    'z'                         \
    "abc"                       \
    out
  assert "$out" is_empty
}

# 38
@test 'match_indices: matches at start and end' {
  local out
  _zsh_easymotion_match_indices \
    'a'                         \
    "abcba"                     \
    out
  assert "$out" same_as "1 5"
}

# 39
@test 'match_indices: regex metacharacters interpreted as pattern' {
  local out
  _zsh_easymotion_match_indices \
    "[aA]"                      \
    "An apple a day"            \
    out
  assert "$out" same_as "1 4 10 13"
}

# 40
@test 'match_indices: consecutive matches found correctly' {
  local out
  _zsh_easymotion_match_indices \
    'a'                         \
    "aaa"                       \
    out
  assert "$out" same_as "1 2 3"
}

# 41
@test 'move_cursor: converts 1-based to 0-based correctly' {
  local CURSOR

  _zsh_easymotion_move_cursor 1
  assert "$CURSOR" same_as "0"

  _zsh_easymotion_move_cursor 5
  assert "$CURSOR" same_as "4"

  _zsh_easymotion_move_cursor 10
  assert "$CURSOR" same_as "9"
}

# 42
@test 'prompt_jump: uses custom prompt from zstyle' {
  zstyle ':zsh-easymotion:*' prompt-key "Jump to> "

  local captured_prompt=""
  function mock_query_char() {
    captured_prompt="$2"
    : ${(P)1::="x"}  # mock user input
  }

  local _fn_query_char=mock_query_char

  local out
  _zsh_easymotion_prompt_jump out

  assert "$captured_prompt" same_as "Jump to> "
  assert "$out" same_as "x"

  zstyle -d ':zsh-easymotion:*' prompt-key
}

# 43
@test 'prompt_search: uses custom prompt from zstyle' {
  zstyle ':zsh-easymotion:*' prompt-char "Search > "

  local captured_prompt=""
  function mock_query_char() {
    captured_prompt="$2"
    : ${(P)1::="x"}  # mock user input
  }

  local _fn_query_char=mock_query_char

  local out
  _zsh_easymotion_prompt_search out

  assert "$captured_prompt" same_as "Search > "
  assert "$out" same_as "x"

  zstyle -d ':zsh-easymotion:*' prompt-char
}

# 44
@test 'readkey: fails on eof' {
  function read() {
    _zsh_easymotion_failure
  }

  local out
  run _zsh_easymotion_readkey out

  assert $state equals 1
}

# 45
@test 'readkey: not printable with mock' {
  function read() {
    local ref="$4"
    : ${(P)ref::="a"}  # mock user input
  }

  local _fn_isprintable=_zsh_easymotion_failure

  local out
  run _zsh_easymotion_readkey out

  assert $state equals 1
}

# 46
@test 'readkey: assign with mock' {
  function read() {
    local ref="$4"
    : ${(P)ref::="é"}  # mock user input
  }

  local _fn_isprintable=_zsh_easymotion_success

  local out
  _zsh_easymotion_readkey out

  assert "$out" same_as "é"
}

# 47
@test 'render_jump_markers: empty input' {
  function zle() {}

  local -a region_highlight
  _zsh_easymotion_render_jump_markers \
    "hello"                           \
    "" 
  assert "${#region_highlight[@]}" equals 1
  assert "$region_highlight[1]" same_as "0 5 fg=black,bold"

  # restores original buffer
  assert "$BUFFER" same_as "hello"
}

# 48
@test 'render_jump_markers: single-character' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a map=(
    "a"$'\0'"2"
    "b"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/x"                           \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 5 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=196,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=196,bold"
  assert "$BUFFER" same_as "xaxbx"
}

# 49
@test 'render_jump_markers: multi-character' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a map=("ab"$'\0'"2")
  _zsh_easymotion_render_jump_markers \
    "x/xx"                            \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 4 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$BUFFER" same_as "xabx"
}

# 50
@test 'render_jump_markers: mixed single and multi-char' {
  function zle() {}

  local BUFFER
  local -a region_highlight
  local -a maps=(
    "a"$'\0'"2"
    "bc"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/xx"                          \
    ""                                \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 4
  assert "$region_highlight[1]" same_as "0 6 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=196,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=208,bold"
  assert "$region_highlight[4]" same_as "4 5 fg=94,bold"
  assert "$BUFFER" same_as "xaxbcx"
}

# 51
@test 'render_jump_markers: overlapping' {
  function zle() {}

  local BUFFER=""
  local -a region_highlight
  local -a maps=(
    ";a"$'\0'"2"
    ";b"$'\0'"3"
  )
  _zsh_easymotion_render_jump_markers \
    "x//x"                            \
    ""                                \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 5
  assert "$region_highlight[1]" same_as "0 4 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$region_highlight[4]" same_as "2 3 fg=208,bold"
  assert "$region_highlight[5]" same_as "3 4 fg=94,bold"
  assert "$BUFFER" same_as "x;;b"
}

# 52
@test 'render_jump_markers: end of buffer' {
  function zle() {}

  local BUFFER=""
  local -a region_highlight
  local -a maps=(
    ";a"$'\0'"2"
  )
  _zsh_easymotion_render_jump_markers \
    "x/"                             \
    ""                               \
    ${maps[@]}
  assert "${#region_highlight[@]}" equals 3
  assert "$region_highlight[1]" same_as "0 2 fg=black,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=208,bold"
  assert "$region_highlight[3]" same_as "2 3 fg=94,bold"
  assert "$BUFFER" same_as "x;a"
}

# 53
@test 'render_jump_markers: respects custom zstyle colors' {
  zstyle ':zsh-easymotion:*' fg-primary 'fg=red,bold'
  zstyle ':zsh-easymotion:*' fg-secondary 'fg=orange,bold'
  zstyle ':zsh-easymotion:*' fg-tertiary 'fg=yellow,bold'
  zstyle ':zsh-easymotion:*' fg-dim 'fg=green,bold'

  function zle() {}

  local -a region_highlight
  local -a map=(
    "a"$'\0'"2"
    ";;"$'\0'"4"
  )
  _zsh_easymotion_render_jump_markers \
    "x/x/x"                           \
    ""                                \
    ${map[@]}
  assert "${#region_highlight[@]}" equals 4
  assert "$region_highlight[1]" same_as "0 5 fg=green,bold"
  assert "$region_highlight[2]" same_as "1 2 fg=red,bold"
  assert "$region_highlight[3]" same_as "3 4 fg=orange,bold"
  assert "$region_highlight[4]" same_as "4 5 fg=yellow,bold"

  zstyle -d ':zsh-easymotion:*' fg-primary
  zstyle -d ':zsh-easymotion:*' fg-secondary
  zstyle -d ':zsh-easymotion:*' fg-tertiary
  zstyle -d ':zsh-easymotion:*' fg-dim
}

# 54
@test 'success: returns exit code 0' {
  run _zsh_easymotion_success
  assert $state equals 0
}
